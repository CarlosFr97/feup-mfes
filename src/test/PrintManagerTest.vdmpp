class PrintManagerTest is subclass of Test

operations


	 	public createEmployee: seq of (char)*seq of (char) ==> [Employee]
 		createEmployee(username, password) ==
 		(
 			dcl manager:PrintManager;
 			dcl emp:[Employee];
 			dcl empCreated: bool;
 			dcl admin: Employee;
 			manager := PrintManager`getInstance();
 			admin := manager.login("admin", "admin");
 			empCreated := manager.addEmployee(username,password,<Regular>);
 			PrintManager`getInstance().logout();
 			emp := manager.login(username, password);
 			return emp;
 		);
 		
	-- Normal scenario for creating a client
	-- as described in section 1.2 of the report, covering requirement R14.	
	public testAddClient: () ==> ()
		testAddClient() ==
		(	
			dcl manager: PrintManager;
			dcl cli: [Client];
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - add client\n");
			cli := manager.addClient("utilizador", "123456778", 10.0);
			assertEqual(manager.getCurrentUser(), cli);
			manager.logout();
			cli := manager.addClient("utilizador", "123456778", 10.0);
			assertEqual(cli, nil);
			
		);
	
	-- Normal scenario for logging out
	-- as described in section 1.2 of the report, covering requirement R15.		
	public testLogout: () ==> ()
		testLogout() ==
		(
			dcl cli: [Client] := PrintManager`getInstance().login("utilizador", "123456778");
			PrintManager`getInstance().logout();
			assertEqual(PrintManager`getInstance().getCurrentUser(), nil);	
		);
	
	-- Exceptional scenario for error logging in
	public testFailLogin: () ==> ()
		testFailLogin() ==
		(
			dcl manager: PrintManager;
			dcl cli: [Client];
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - fail login\n");
			cli := manager.addClient("utilizador", "123456778", 10.0);
			assertEqual(manager.getCurrentUser(), cli);
			PrintManager`getInstance().logout();
			cli := PrintManager`getInstance().addClient("utilizador", "123456778", 10.0);
			assertEqual(cli, nil);
			cli := PrintManager`getInstance().login("utilizador", "1234567");
			assertEqual(cli, nil);
		);
	
	-- Normal scenario for adding multiple clients in order to test is clients exists verification is properly working
	public testAddMultipleClient: () ==> ()
		testAddMultipleClient() ==
		(	
			dcl manager: PrintManager;
			dcl cli: Client;
			dcl cli2: Client;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - add multiple clients\n");
			cli := manager.addClient("utilizador", "123456778", 10.0);
			assertEqual(manager.getCurrentUser(), cli);
			PrintManager`getInstance().logout();
			assertEqual(manager.getCurrentUser(), nil);
			cli2 := manager.addClient("utilizador2", "utiliza123456", 10.0);
			assertEqual(manager.getCurrentUser(), cli2);
			
		);
		
	-- Normal scenario for ladding an employee
	-- as described in section 1.2 of the report, covering requirement R13.			
	public testAddEmployee: () ==> ()
		testAddEmployee() ==
		(	
			dcl manager: PrintManager;
			dcl emp: [Employee];
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - add employee\n");
			emp := createEmployee("empregado", "emp123456778");
			assertEqual(manager.getCurrentUser(), emp);
			manager.logout();
			emp := createEmployee("empregado", "123456778");
			assertEqual(emp, nil);
		);
	
		
	-- Normal scenario for adding a printing queue
	-- as described in section 1.2 of the report, covering requirement R1.			
	public testAddQueue: () ==> ()
		testAddQueue() ==
		(
			dcl manager: PrintManager;
			dcl emp: Employee;
			dcl queue: Queue;
			dcl queue2: Queue;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - add queue\n");
			emp := createEmployee("empregado", "emp123456778");
			queue := manager.addQueue(<A4>,<BlackWhite>);
			queue2 := manager.addQueue(<A3>,<Colors>);
			assertEqual(manager.getQueues(),{queue, queue2});
		);
	
	-- Normal scenario for adding a printer
	-- as described in section 1.2 of the report, covering requirement R2.		
	public testAddPrinter: () ==> ()
		testAddPrinter() ==
		(
			dcl manager: PrintManager;
			dcl emp: Employee;
			dcl queue: Queue;
			dcl queue2: Queue;
			dcl printer: Printer;
			dcl printer2: Printer;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - add printer\n");
			emp := createEmployee("empregado", "emp123456778");
			queue := manager.addQueue(<A4>,<BlackWhite>);
			queue2 := manager.addQueue(<A3>,<Colors>);
			printer := manager.addPrinter(0, "piso1", {queue});
			printer2 := manager.addPrinter(1,"piso2", {queue2, queue});
			assertEqual(manager.getPrinters(),{printer, printer2});
		);
	
	-- Normal scenario for getting a report of all clients
	-- as described in section 1.2 of the report, covering requirement R11.		
	public testReportClients: () ==> ()
		testReportClients() ==
		(
			dcl manager: PrintManager;
			dcl emp: Employee;
			dcl admin: Employee;
			dcl cli1: Client;
			dcl cli2: Client;
			dcl doc1: Document;
			dcl doc2: Document;
			dcl doc3: Document;
			dcl queue: Queue;
			dcl queue2: Queue;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - report clients\n");
			emp := createEmployee("empregado", "emp123456778");
			queue := manager.addQueue(<A4>,<BlackWhite>);
			queue2 := manager.addQueue(<A3>,<Colors>);
			PrintManager`getInstance().logout();
			cli1 := manager.addClient("client1", "12345678", 10.0);
			doc1 := cli1.addDocument(<A4>, <BlackWhite>, <Portrait>, "trabalho1", mk_Document`Date(21,12,2014), 2);
			doc2 := cli1.addDocument(<A3>, <Colors>, <Portrait>, "trabalho2", mk_Document`Date(21,12,2014), 2);
			PrintManager`getInstance().logout();
			cli2 := manager.addClient("client2", "12345678", 10.0);
			doc3 := cli2.addDocument(<A4>, <BlackWhite>, <Landscape>, "trabalho3", mk_Document`Date(21,12,2014), 3);
			PrintManager`getInstance().logout();
			admin := manager.login("admin", "admin");
			assertEqual(manager.reportClients(), {"client1" |-> {doc1, doc2}, "client2" |-> {doc3}});
		);
		
	-- Normal scenario for getting a report of the number of printed documents by queue
	-- as described in section 1.2 of the report, covering requirement R9.			
	public testReportPrintedDocs: () ==> ()
		testReportPrintedDocs() ==
		(
			dcl manager: PrintManager;
			dcl admin: Employee;
			dcl cli1: Client;
			dcl queue: Queue;
			dcl queue2: Queue;
			dcl printer: Printer;
			dcl printer2: Printer;
			dcl doc1: Document;
			dcl doc2: Document;
			dcl doc3: Document;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - report printed docs\n");
			admin := manager.login("admin", "admin");
			queue := manager.addQueue(<A4>,<BlackWhite>);
			queue2 := manager.addQueue(<A4>,<Colors>);
			printer := manager.addPrinter(0, "piso1", {queue});
			printer2 := manager.addPrinter(1,"piso2", {queue2, queue});
			manager.logout();
			cli1 := manager.addClient("utilizador", "utiliza123456", 10.0);
			doc1 := cli1.addDocument(<A4>, <BlackWhite>, <Portrait>, "trabalho1", mk_Document`Date(21,12,2014), 2);
			doc2 := cli1.addDocument(<A4>, <Colors>, <Portrait>, "trabalho2", mk_Document`Date(21,12,2014), 2);
			doc3 := cli1.addDocument(<A4>, <BlackWhite>, <Landscape>, "trabalho3", mk_Document`Date(21,12,2014), 3);
			printer.print(doc1);
			printer2.print(doc2);
			printer2.print(doc3);
			manager.logout();
			admin := manager.login("admin", "admin");
			assertEqual(manager.reportPrintedDocs(), {queue |-> 2, queue2 |-> 1});
			
);
	-- Normal scenario for getting a report of an employee work
	-- as described in section 1.2 of the report, covering requirement R10.		
	public testReportEmployees: () ==> ()
		testReportEmployees() ==
		(
			dcl manager: PrintManager;
			dcl admin: Employee;
			dcl cli1: Client;
			dcl queue: Queue;
			dcl queue2: Queue;
			dcl printer: Printer;
			dcl printer2: Printer;
			dcl malf: Malfunction;
			dcl report: map Malfunction`malfunctionState to int := {|->};
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - report employee\n");
			admin := manager.login("admin", "admin");
			queue := manager.addQueue(<A4>,<BlackWhite>);
			queue2 := manager.addQueue(<A3>,<Colors>);
			printer := manager.addPrinter(0, "piso1", {queue});
			printer2 := manager.addPrinter(1,"piso2", {queue2, queue});
			manager.logout();
			cli1 := manager.addClient("utilizador", "utiliza123456", 10.0);
			malf := cli1.reportMalfunction(printer, <Paper>, "problem");
			manager.logout();
			admin := manager.login("admin", "admin");
			admin.assignMalfunction(malf, admin);
			report := manager.reportEmployee(admin);
			assertEqual({<InRepair> |-> 1, <Fixed> |-> 0}, report);
			malf.changeState(<Fixed>);
			report := manager.reportEmployee(admin);
			assertEqual({<InRepair> |-> 0, <Fixed> |-> 1}, report);
		);
	
	-- Normal scenario for getting all the existing malfunctions
	-- as described in section 1.2 of the report, covering requirement R8.			
	public testMalfunctions: () ==> ()
		testMalfunctions() ==
		(
			dcl manager: PrintManager;
			dcl emp: Employee;
			dcl malf: Malfunction;
			dcl queue: Queue;
			dcl printer: Printer;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - get malfunctions\n");
			emp := createEmployee("empregado", "emp123456778");
			queue := manager.addQueue(<A4>,<BlackWhite>);
			printer := manager.addPrinter(0, "piso1", {queue});
			malf := emp.reportMalfunction(printer, <Paper>, "problem");
			assertEqual(manager.getMalfunctions(), {malf});
		
		);
		
	-- Normal scenario for getting all the regular employees
	public testGetRegularEmployees: () ==> ()
			testGetRegularEmployees() ==
			(
				dcl manager: PrintManager;
				dcl emp: Employee;
				dcl emp2: Employee;
				dcl admin: Employee;
				PrintManager`clearInstance();
				manager := PrintManager`getInstance();
				IO`print("Test PrintManager - get regular employees\n");
				emp := createEmployee("empregado", "emp123456778");
				PrintManager`getInstance().logout();
				emp2 := createEmployee("empregado2", "emp1234567782");
				PrintManager`getInstance().logout();
				admin := manager.login("admin", "admin");
				assertEqual(manager.getRegularEmployees(), {emp, emp2});
			);
	
	-- Normal scenario for getting a specific employee
	public testGetSpecificEmployee: () ==> ()
		testGetSpecificEmployee() ==
		(
			dcl manager: PrintManager;
			dcl emp: Employee;
			dcl admin: Employee;
			PrintManager`clearInstance();
			manager := PrintManager`getInstance();
			IO`print("Test PrintManager - get specific employee\n");
			emp := createEmployee("empregado", "emp123456778");
			PrintManager`getInstance().logout();
			admin := manager.login("admin", "admin");
			assertEqual(manager.getEmployee("manuel"), nil);
			assertEqual(manager.getEmployee("empregado"), emp);
		);
	

	public test: () ==> ()
		test() ==
		(
			testAddClient();
			testLogout();
			testFailLogin();
			testAddMultipleClient();
			testAddEmployee();
			testAddQueue();
			testAddPrinter();
			testReportClients();
			testReportPrintedDocs();
			testReportEmployees();
			testMalfunctions();
			testGetRegularEmployees();
			testGetSpecificEmployee();
		)
end PrintManagerTest