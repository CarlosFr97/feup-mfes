class ClientTest is subclass of Test

operations
 	public createClient: () ==> Client
 		createClient() ==
 		(
 			dcl manager:PrintManager;
 			dcl cli:Client;
 			manager := PrintManager`getInstance();
 			cli := manager.addClient("utilizador","12345678",10.0);
 			return cli;
 		);
 		
 	public createEmployee: () ==> Employee
 		createEmployee() ==
 		(
 			dcl manager:PrintManager;
 			dcl emp:Employee;
 			dcl empCreated: bool;
 			dcl admin: Employee;
 			manager := PrintManager`getInstance();
 			admin := manager.login("admin", "admin");
 			empCreated := manager.addEmployee("empregado","emp12345",<Regular>);
 			assertTrue(empCreated);
 			PrintManager`getInstance().logout();
 			emp := manager.login("empregado", "emp12345");
 			return emp;
 		);
 		
 	public createQueue: () ==> Queue
 		createQueue() ==
 		(
 				return PrintManager`getInstance().addQueue(<A4>,<BlackWhite>);
 		);
 	
 	-- Normal scenario for loading (adding) 
	-- as described in section 1.2 of the report, covering requirement R14.	
 	public testClient: () ==> ()
 		testClient() ==
 			(
 				dcl cli:Client;
 				PrintManager`clearInstance();
 				IO`print("Test Client - create\n");
 				cli := createClient();
 				assertEqual(cli.getAccount(), 10.0);
 			);
 	
 	-- Normal scenario for adding money to a client's account
	-- as described in section 1.2 of the report, covering requirement R12.				
 	public testAddMoney: () ==> ()
 		testAddMoney() ==
 			(
 				dcl money:real := 5.0;
 				dcl cli:Client;
 				PrintManager`clearInstance();
 				IO`print("Test Client - deposit money\n");
 				cli := createClient();
 				cli.depositMoney(money);
 				assertEqual(cli.getAccount(),15.0);
 			);
 			
 	-- Normal scenario for creating and deleting a client document
	-- as described in section 1.2 of the report, covering requirement R3 and R16.			
 	public testCreateAndDeleteDocument: () ==> ()
 		testCreateAndDeleteDocument() ==
 			(
 				dcl emp: Employee;
 				dcl queue: Queue; 
 				dcl cli:Client;
 				dcl doc: Document;
 				dcl tempDoc: [Document];
 				PrintManager`clearInstance();
 				IO`print("Test Client - create document\n");
 				cli := createClient();
 				assertEqual(cli.addDocument(<A4>,<BlackWhite>,<Portrait>,"Teste", mk_Document`Date(21,12,2014), 2), nil);
 				PrintManager`getInstance().logout();
 				emp := createEmployee();
 				queue := createQueue();
				PrintManager`getInstance().logout();
 				cli := PrintManager`getInstance().login("utilizador","12345678");
 				 -- testing add document
 				doc := cli.addDocument(<A4>,<BlackWhite>,<Portrait>,"Teste", mk_Document`Date(21,12,2014), 2);
 				assertNotEqual(doc, nil);
 				assertEqual(cli.getDocs(), {doc});
 				tempDoc := cli.addDocument(<A4>,<BlackWhite>,<Portrait>,"Teste", mk_Document`Date(21,12,2014), 2);
 				assertEqual(tempDoc, nil);
 				PrintManager`getInstance().logout();
 				emp := PrintManager`getInstance().login("admin","admin");
 				assertEqual(cli.getDocs(), {doc});
 				PrintManager`getInstance().logout();
 				
 				 -- testing delete
 				cli := PrintManager`getInstance().login("utilizador","12345678");
 				cli.deleteDoc(doc);
 				assertEqual(cli.getDocs(), {});
 			);
 			
 		-- Exceptional scenario for creating a document without creating the queue it needs	
 	 	public testCreateDocumentWithoutProperQueue: () ==> ()
 		testCreateDocumentWithoutProperQueue() ==
 			(
 				dcl cli:Client;
 				PrintManager`clearInstance();
 				IO`print("Test Client - create document without proper queue\n");
 				cli := createClient();
 				assertEqual(cli.addDocument(<A4>,<BlackWhite>,<Portrait>,"Teste", mk_Document`Date(21,12,2014), 2), nil);
 			);
 	
 	-- Normal scenario for paying a document when it is printed
	-- as described in section 1.2 of the report, covering requirement R4.		
 	public testPayDocument: () ==> ()
 		testPayDocument() ==
 			(
 				dcl emp: Employee; 
 				dcl queue: Queue; 
 				dcl cli:Client; 
 				dcl acc:real; 
 				dcl doc:Document;
 				PrintManager`clearInstance();
 				IO`print("Test Client - pay document\n");
 				emp := createEmployee();
 				queue := createQueue();
				PrintManager`getInstance().logout();
 				cli := createClient();
 				acc := cli.getAccount();
 				doc := cli.addDocument(<A4>,<BlackWhite>, <Portrait>, "Teste", mk_Document`Date(21,12,2014), 2);
 				cli.payDocument(doc);
 				assertEqual(cli.getAccount(), acc - doc.getPrice());
 			);
	
 	public test: () ==> ()
 		test() ==
 			(
 				testClient();
 				testAddMoney();
 				testCreateAndDeleteDocument();
 				testPayDocument();
 			);
 		

end ClientTest