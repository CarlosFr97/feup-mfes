class Document
instance variables
  client:Client;
  size:documentSize := <A4>;
  color:documentColor := <BlackWhite>;
  type:documentType := <Portrait>;
  numPages: nat1 := 1;
  price:real := 0;
  state: documentState := <ToPrint>;
  name: seq of (char);
  date: Date;
  queue: [Queue] := nil;
 
types
	public documentSize = <A3> | <A4>;
	public documentColor = <BlackWhite> | <Colors>;
	public documentType = <Portrait> | <Landscape>;
	public documentState = <Printed> | <ToPrint>;	
	public Date:: day: nat1
								month: nat1
								year: nat1
								
	inv date == if date.year mod 400 = 0 or (date.year mod 100 <> 0 and date.year mod 4 <> 0)
				then date.month <= 12 and date.day <= [31, 29, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31](date.month)
				else date.month <= 12 and date.day <= [31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31](date.month);
	
operations

	public Document : Client * documentSize * documentColor * documentType * seq of (char) * Date * nat1 ==> Document
		Document(cli,siz,col,ty, nam, dat, nPages) == 
			(
				client := cli;
				size := siz;
				color := col;
				type := ty;
				name := nam;
				date := dat;
				numPages := nPages;
				calculatePrice();
				return self;
			)
			pre PrintManager`getInstance().getCurrentUser() = cli;
	
	public setQueue: Queue ==> () 
		setQueue(q) ==
			queue := q
		pre q in set PrintManager`getInstance().getQueues()
		post queue = q;
		
	public getName: () ==> seq of (char)
		getName() ==
			return name
		pre PrintManager`getInstance().getCurrentUser() = client or PrintManager`getInstance().getCurrentUser().getRole() <> <Client>;
		
	public getDate: () ==> Date
		getDate() ==
			return date
		pre PrintManager`getInstance().getCurrentUser() = client or PrintManager`getInstance().getCurrentUser().getRole() <> <Client>;
		
	public removeFromQueue: () ==> Queue
		removeFromQueue() ==
			(
				queue.removeDocument(self);
				return queue;
			);

	public pure getPrice: () ==> real
		getPrice() ==
			return price
		pre PrintManager`getInstance().getCurrentUser() = client;

	public pure getClient: () ==> Client
		getClient() ==
			return client
		pre PrintManager`getInstance().getCurrentUser().getRole() <> <Client> or PrintManager`getInstance().getCurrentUser() = client;
			
	public getColor: () ==> documentColor
		getColor() ==
			return color;		
			
	public getSize: () ==> documentSize
		getSize() ==
			return size;		
	
	public pure getQueue: () ==> [Queue]
		getQueue() ==
			return queue;
			
	private calculatePrice: () ==> ()
		calculatePrice() ==
			(
				cases true:
					(size = <A3> and color = <BlackWhite>) -> price := 0.1*numPages,
					(size = <A3> and color = <Colors>) -> price := 0.20*numPages,
					(size = <A4> and color = <BlackWhite>) -> price := 0.03*numPages,
					(size = <A4> and color = <Colors>) -> price := 0.05*numPages
				end;
			)
			pre numPages >=1;

end Document
