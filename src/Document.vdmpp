class Document
instance variables
  client:Client;
  size:documentSize := <A4>;
  color:documentColor := <BlackWhite>;
  type:documentType := <Portrait>;
  price:real := 0;
  state: documentState := <ToPrint>;
  name: seq of (char);
  date: Date;
  queue: [Queue] := nil;
 
types
	public documentSize = <A3> | <A4>;
	public documentColor = <BlackWhite> | <Colors>;
	public documentType = <Portrait> | <Landscape>;
	public documentState = <Printed> | <ToPrint>;	
	public Date:: day: nat1
								month: nat1
								year: nat1
								
	inv date == if date.year mod 400 = 0 or (date.year mod 100 <> 0 and date.year mod 4 <> 0)
				then date.month <= 12 and date.day <= [31, 29, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31](date.month)
				else date.month <= 12 and date.day <= [31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31](date.month);
	
operations

	public Document : Client * documentSize * documentColor * documentType * seq of (char) * Date ==> Document
		Document(cli,siz,col,ty, nam, dat) == 
			(
				client := cli;
				size := siz;
				color := col;
				type := ty;
				name := nam;
				date := dat;
				calculatePrice();
				return self;
			)
			pre PrintManager`getInstance().getCurrentUser() = cli;
	
	public setQueue: Queue ==> () 
		setQueue(q) ==
			queue := q
		pre q in set PrintManager`getInstance().getQueues()
		post queue = q;
		
	public removeFromQueue: () ==> ()
		removeFromQueue() ==
			queue.removeDocument(self);	

	public pure getPrice: () ==> real
		getPrice() ==
			return price
		pre PrintManager`getInstance().getCurrentUser() = client;

	public pure getClient: () ==> Client
		getClient() ==
			return client
		pre PrintManager`getInstance().getCurrentUser().getRole() <> <Client> or PrintManager`getInstance().getCurrentUser() = client;
			
	public getColor: () ==> documentColor
		getColor() ==
			return color;		
			
	public getSize: () ==> documentSize
		getSize() ==
			return size;		
	
	public pure getQueue: () ==> [Queue]
		getQueue() ==
			return queue;
			
	private calculatePrice: () ==> ()
		calculatePrice() ==
			(
				calculateSize();
				calculateColor();
			);
		
	private calculateSize: () ==> ()
		calculateSize() ==
			cases size: 
				(<A3>) -> price := price + 0.2,
				(<A4>) -> price := price + 0.1
			end;
	
	private calculateColor: () ==> ()
		calculateColor() ==
			cases color: 
				(<BlackWhite>) -> price := price + 0.1,
				(<Colors>) -> price := price + 0.25
			end;		

end Document
