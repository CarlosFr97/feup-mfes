class Printer
instance variables
  id:nat := 0;
  queues: map Queue to nat := {|->};
  location:seq of (char) := [];
  

operations
	public Printer: nat * seq of (char) * set of Queue ==> Printer
		Printer(identifier,local,lists) ==
			(
				queues := queues munion {queue |-> 0 | queue in set lists};
				id := identifier;
				location := local;
				return self;
			);
			
	public pure getId: () ==> nat
		getId() ==
			return id
		pre isofclass(Employee, PrintManager`getInstance().getCurrentUser());
	
	public pure getLocation: () ==> seq of (char)
		getLocation() ==
			return location
		pre PrintManager`getInstance().getCurrentUser() <> nil;
			
  public pure queryPrintAsClient: ()  ==> set of Document
	  queryPrintAsClient() ==
	  (
		   	dcl docs: set of Document := {};
		   	for all queue in set dom queues do (
		   		docs := docs union queue.getClientDocs();
		   	);
		   	return docs;
	  )
	  pre PrintManager`getInstance().getCurrentUser() <> nil and isofclass(Client, PrintManager`getInstance().getCurrentUser());
	  
	public queryPrint: ()  ==> set of Document
	  queryPrint() ==
	  (
		   	dcl docs: set of Document := {};
		   	for all queue in set dom queues do (
		   		docs := docs union queue.getDocs();
		   	);
		   	return docs;
	  )
	  pre isofclass(Employee,PrintManager`getInstance().getCurrentUser());
   
 public print: Document ==> ()
		print(doc) ==
		(
			dcl client: Client := doc.getClient();
			dcl newQueue: Queue;
			client.payDocument(doc);
			newQueue := doc.removeFromQueue();
			queues := queues ++ {newQueue |-> queues(newQueue)+1};
			doc.print();
		)
		pre doc.getClient() = PrintManager`getInstance().getCurrentUser() and doc in set doc.getQueue().getClientDocs() and doc in set queryPrintAsClient() and doc.getPrinted() = <ToPrint>
		post doc.getPrinted() = <Printed>;
		

 public getNumPrintedDocs: () ==> map Queue to int
		getNumPrintedDocs() ==
			return queues;
			

end Printer
