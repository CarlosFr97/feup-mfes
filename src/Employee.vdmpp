class Employee is subclass of User
instance variables
  malfunctions:set of (Malfunction) := {};
 	role: Role;
 	
types
	public Role = <Regular> | <Admin>; 

operations
	
	public Employee: seq of (char) * seq of (char) * Role ==> Employee
		Employee(username, password, newRole) ==
			(
				initUser(username, password);
				role := newRole;
			);
			
	public pure getRole: () ==> Role
		getRole() ==
			return role;
				
	public addMalfunction: Malfunction ==> ()
		addMalfunction(malf) ==
		( 
			malfunctions := malfunctions union {malf}
		)
		post malf in set malfunctions;
		
	
	public pure getMalfunctions: () ==> set of (Malfunction)
		getMalfunctions() ==
			return malfunctions
		pre (isofclass(Employee, PrintManager`getInstance().getCurrentUser()) and narrow_(PrintManager`getInstance().getCurrentUser(), Employee).getRole() = <Admin>) or PrintManager`getInstance().getCurrentUser() = self;
		
	
	-- only as an admin
	public assignMalfunction: Malfunction * Employee ==> ()
		assignMalfunction(malf, emp) ==
		(
			malf.assignEmployee(emp);
			emp.addMalfunction(malf);
		)
		pre isofclass(Employee, PrintManager`getInstance().getCurrentUser()) and narrow_(PrintManager`getInstance().getCurrentUser(), Employee).getRole() = <Admin>
		post malf in set emp.getMalfunctions();
		
end Employee
