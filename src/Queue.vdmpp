class Queue
instance variables
  documents:set of (Document) := {};
  size:Document`documentSize;
  color:Document`documentColor;


operations

	public Queue: Document`documentSize*Document`documentColor ==> Queue
		Queue(newSize, newColor) == 
		(
				size := newSize;
				color := newColor;
				return self;
		);
	
	public getSize: () ==> Document`documentSize
		getSize() ==
			return size;
	
	public getColor: () ==> Document`documentColor
		getColor() ==
			return color;
	
	public addDocument: Document ==> ()
		addDocument(d) ==
			documents := documents union  {d}
		pre d not in set documents
		post d in set documents;
	
	public removeDocument: Document ==> ()
		removeDocument(d) ==
			documents := documents \ {d}
		pre d in set documents
		post d not in set documents;
		
	public pure getClientDocs: () ==> set of (Document)
		getClientDocs() ==
		(	
			dcl allDocs: set of (Document) := {};
		  dcl client: User := PrintManager`getInstance().getCurrentUser();
			for all document in set documents do
			(
				if document.getClient() = client then
					allDocs := allDocs union {document};
			);
			return allDocs;
		)
		pre PrintManager`getInstance().getCurrentUser() <> nil and PrintManager`getInstance().getCurrentUser().getRole() = <Client>;
	
	public pure getDocs: () ==> set of (Document)
		getDocs() ==
			return documents;
		
			
end Queue
