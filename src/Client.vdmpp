class Client is subclass of User
instance variables
  malfunctions:set of (Malfunction) := {};
  account:real := 0;

	inv account >= 0
operations

	public Client:seq of (char) * seq of (char) * real ==> Client
		Client(username,pass,acc) == 
			(
				initUser(username, pass, <Client>);
				account := acc;
				return self;
			)
			pre acc > 0
			post account >= 0.2 and account = account~ + acc;
	
	--accounting
	
	public depositMoney: real ==> ()
		depositMoney(value) ==
			(
				account := account + value;
			)
			pre value > 0 and PrintManager`getInstance().getCurrentUser() = self
			post account = account~ + value; 
		
		
	public  payDocument : Document  ==> ()
  	payDocument(d) ==
	  	(
	  	 	account := account - d.getPrice();
	  	)
			pre d.getClient() = self and account > d.getPrice() and PrintManager`getInstance().getCurrentUser() = self
			post account = account~ - d.getPrice();
		

 --actions
 
	public addDocument: Document`documentSize * Document`documentType * Document`documentColor * seq of (char) * Document`Date ==> [Document]
		addDocument(size,color, type, name, date) == 
		(
			dcl newDoc :Document := new Document(self, size,type,color, name, date);
			if PrintManager`getInstance().addDocumentToQueue(newDoc) then 
				return newDoc	
			else
				return nil;
		)
		pre PrintManager`getInstance().getCurrentUser() = self;
		
	
	public reportMalfunction: Printer * Malfunction`problemType * [seq of (char)] ==> Malfunction
		reportMalfunction(printer,type,desc) ==
		(
			dcl malf :Malfunction := new Malfunction(self,printer,type,desc);
			printer.addMalfunction(malf);
			malfunctions := malfunctions union {malf};
			return malf
		)
		pre printer in set PrintManager`getInstance().getPrinters() and PrintManager`getInstance().getCurrentUser() = self; 
		
	-- getters
		
	public pure getAccount: () ==> real
		getAccount() ==
			return account
		pre PrintManager`getInstance().getCurrentUser() = self;
		
	public getMalfunctions: () ==> set of (Malfunction)
		getMalfunctions() ==
			return malfunctions
		pre PrintManager`getInstance().getCurrentUser() = self;
	
end Client
