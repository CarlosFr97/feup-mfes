class Client is subclass of User
instance variables
  account:real := 0;
	documents: set of Document := {};

	inv account >= 0
operations

	public Client:seq of (char) * seq of (char) * real ==> Client
		Client(username,pass,acc) == 
			(
				initUser(username, pass);
				account := acc;
				return self;
			)
			pre acc > 0
			post account >= 0.2 and account = account~ + acc;
	
	--accounting
	
	public depositMoney: real ==> ()
		depositMoney(value) ==
			(
				account := account + value;
			)
			pre value > 0 and PrintManager`getInstance().getCurrentUser() = self
			post account = account~ + value; 
		
		
	public  payDocument : Document  ==> ()
  	payDocument(d) ==
	  	(
	  	 	account := account - d.getPrice();
	  	)
			pre d.getClient() = self and account > d.getPrice() and PrintManager`getInstance().getCurrentUser() = self
			post account = account~ - d.getPrice();
		

 --actions
 
	public addDocument: Document`documentSize * Document`documentColor * Document`documentType * seq of (char) * Document`Date * nat1 ==> [Document]
		addDocument(size, color, type, name, date, numPages) == 
		(
			dcl newDoc :Document; 
			for all doc in set documents do 
				if name = doc.getName() and date = doc.getDate() then
					return nil;
					
			newDoc := new Document(self, size, color, type, name, date, numPages);
			if PrintManager`getInstance().addDocumentToQueue(newDoc) then
			(
				documents := documents union {newDoc}; 
				return newDoc;
			)	
			else
				return nil;
		)
		pre PrintManager`getInstance().getCurrentUser() = self;
		
	-- getters
		
	public pure getAccount: () ==> real
		getAccount() ==
			return account
		pre PrintManager`getInstance().getCurrentUser() = self;
	
	public getDocs: () ==> set of Document
		getDocs() ==
			return documents
		pre PrintManager`getInstance().getCurrentUser() = self or (isofclass(Employee, PrintManager`getInstance().getCurrentUser()) and narrow_(PrintManager`getInstance().getCurrentUser(), Employee).getRole() = <Admin>);
	
end Client
